module wptr_full #(parameter ADDR_WIDTH = 4)(input wr_clk,
                 input wr_rst_n,
                 input wr_en,
                 input [ADDR_WIDTH:0] rd_ptr_gray_sync,
                 output reg wr_full,
                 output [ADDR_WIDTH-1:0] wr_addr,
                 output reg [ADDR_WIDTH:0] wr_gray
);
reg [ADDR_WIDTH:0] wr_ptr_bin;
reg [ADDR_WIDTH:0] wr_ptr_bin_next;
reg [ADDR_WIDTH:0] wr_ptr_gray_next;
reg wr_full_next;

assign wr_addr = wr_ptr_bin[ADDR_WIDTH-1:0];

always @(*) begin
    if(wr_en && !wr_full) begin
        wr_ptr_bin_next = wr_ptr_bin + 1;
    end
    else begin
        wr_ptr_bin_next = wr_ptr_bin;
    end
    wr_ptr_gray_next = wr_ptr_bin_next ^ (wr_ptr_bin_next>>1);
    wr_full_next = (wr_ptr_gray_next == {~rd_ptr_gray_sync[ADDR_WIDTH:ADDR_WIDTH-1], rd_ptr_gray_sync[ADDR_WIDTH-2:0] });
end

always @(posedge wr_clk or negedge wr_rst_n) begin
    if(!wr_rst_n) begin
        wr_ptr_bin <= 0;
        wr_gray <= 0;
        wr_full <= 0;
    end
    else begin
        wr_ptr_bin <= wr_ptr_bin_next;
        wr_gray <= wr_ptr_gray_next;
        wr_full <= wr_full_next;
    end
end
endmodule